---
layout: post
title: CrackMe:crkme1-win32
date: 2023-01-31
categories: crackme 
---

I have heard of Z3, an SMT solver, and wanted to find more about it. Before I could use Z3, I needed to find a binary that I could practice with.  I browsed the internet and discovered a YouTube video where a specific binary was used. I decided to choose the crack me, crkme1-win32, by Damo2k. It was published on January 12, 2008, and with a difficulty of 2. The crack me was similar to the binary used in the YouTube video, and it was intended to be simple— it was perfect to practice and learn more about Z3.

## The Challenge

`The instruction is to find a valid serial. Patching is not allowed. The executable is not packed or encrypted.`

The crack me consists of a single executable crkme1-win32.exe. When the executable is run, it tells us to pass in a serial as an argument: 

```
Usage: %s <serial>\n”. 
```

Typing a random string  as an argument(e.g. abcdef), the application responds with “Bad Serial!”

```
Bad Serial!
```

## Static Analysis

When I opened the binary in IDA Pro, it did not reveal any signs of packing as all of the standard segments (.text, . data, etc.) were present and contained un-obfuscated instructions and data. 

```nasm
.text:00401000 ; int __cdecl main(int argc, const char **argv, const char **envp)
.text:00401000 _main           proc near               ; CODE XREF: start+AF↓p
.text:00401000
.text:00401000 argc            = dword ptr  4
.text:00401000 argv            = dword ptr  8
.text:00401000 envp            = dword ptr  0Ch
.text:00401000
.text:00401000                 mov     eax, [esp+argc]
.text:00401004                 push    ebx
.text:00401005                 push    esi
.text:00401006                 xor     esi, esi
.text:00401008                 cmp     eax, 1
.text:0040100B                 jle     PrintUsage      ; jumps if argument is not given
```

The program checks if there is an argument passed. If there is none, then it jumps  to the function that tells the user the correct usage of the program (PrintUsage). We have encountered  that it will print “Usage: %s <serial>\n” previously.

```nasm
.text:00401011                 mov     ebx, [esp+8+argv]
.text:00401015                 push    edi
.text:00401016                 or      ecx, 0FFFFFFFFh
.text:00401019                 xor     eax, eax
.text:0040101B                 mov     edx, [ebx+4]
.text:0040101E                 mov     edi, edx
.text:00401020                 repne scasb
.text:00401022                 not     ecx
.text:00401024                 dec     ecx
.text:00401025                 pop     edi
.text:00401026                 cmp     ecx, 0Ah        ; check if serial length is 10
.text:00401029                 jz      short loc_401042 ; serial's length must be 10 to continue
```

After checking if an argument has been passed, the program will scan the string.  repne scasb will scan bytes of the argument string until the trailing null character is found. It will stop and  compare if the number of characters is equal to 10.  If it is , then we will continue and jump to loc_401042.  Otherwise we continue to PrintBadSerial. 

```nasm
.text:0040102B PrintBadSerial:                         ; CODE XREF: _main+70↓j
.text:0040102B                                         ; _main+7F↓j ...
.text:0040102B                 push    offset aBadSerial ; "Bad Serial!"
.text:00401030                 push    offset Format   ; "%s\n"
.text:00401035                 call    _printf
.text:0040103A                 add     esp, 8
.text:0040103D                 xor     eax, eax
.text:0040103F                 pop     esi
.text:00401040                 pop     ebx
.text:00401041                 retn
```

It stops the program after printing “Bad Serial!”. We will not be able to continue.

Our string argument, or serial argument, that we passed must be of length 10 so that we can continue to loc_401042.

```nasm

.text:00401042 loc_401042:                             ; CODE XREF: _main+29↑j
.text:00401042                 cmp     byte ptr [edx], 0 ; check if serial is not equal to 0
.text:00401045                 jz      short loc_401061
.text:00401047
.text:00401047 loc_401047:                             ; CODE XREF: _main+5F↓j
.text:00401047                 mov     al, [edx+esi]
.text:0040104A                 cmp     al, 61h ; 'a'
.text:0040104C                 jl      short loc_401057
.text:0040104E                 cmp     al, 7Ah ; 'z'   ; char must be between 0x61 and 0x7a
.text:00401050                 jg      short loc_401057
.text:00401052                 sub     al, 20h ; ' '
.text:00401054                 mov     [edx+esi], al   ; subtract char by 0x20
.text:00401057
.text:00401057 loc_401057:                             ; CODE XREF: _main+4C↑j
.text:00401057                                         ; _main+50↑j
.text:00401057                 mov     edx, [ebx+4]
.text:0040105A                 inc     esi
.text:0040105B                 cmp     byte ptr [edx+esi], 0 ; until nullbyte
.text:0040105F                 jnz     short loc_401047
```

The program iterates through the serial argument, and checks if each character is between 0x61 and 0x7a. If it is, then the character is subtracted by 0x20.  

```nasm
.text:00401061 loc_401061:                             ; CODE XREF: _main+45↑j
.text:00401061                 mov     ecx, [ebx+4]
.text:00401064                 movsx   eax, byte ptr [ecx+9]
.text:00401068                 movsx   esi, byte ptr [ecx]
.text:0040106B                 sub     eax, 3
.text:0040106E                 cmp     esi, eax        ; serial[0] == serial[9]-3
.text:00401070                 jnz     short PrintBadSerial
.text:00401072                 movsx   edx, byte ptr [ecx+8]
.text:00401076                 movsx   eax, byte ptr [ecx+1]
.text:0040107A                 add     edx, 14
.text:0040107D                 cmp     eax, edx        ; serial[1] == serial[8] + 14
.text:0040107F                 jnz     short PrintBadSerial
.text:00401081                 movsx   edx, byte ptr [ecx+7]
.text:00401085                 movsx   eax, byte ptr [ecx+2]
.text:00401089                 sub     edx, 20
.text:0040108C                 cmp     eax, edx        ; serial[2] == serial[7]-20
.text:0040108E                 jnz     short PrintBadSerial
.text:00401090                 movsx   edx, byte ptr [ecx+6]
.text:00401094                 movsx   eax, byte ptr [ecx+3]
.text:00401098                 add     edx, 6
.text:0040109B                 cmp     eax, edx        ; serial[3] == serial[6]+ 6
.text:0040109D                 jnz     short PrintBadSerial
.text:0040109F                 movsx   eax, byte ptr [ecx+5] ; eax (serial[5])
.text:004010A3                 movsx   ecx, byte ptr [ecx+4] ; ecx (serial[4])
.text:004010A7                 add     eax, ecx        ; serial[5]+ serial[4
.text:004010A9                 cdq                     ; edx = 0x00000000
.text:004010AA                 sub     eax, edx        ; 
.text:004010AC                 sar     eax, 1          ; ( serial[5]+serial[4])  >> 1
.text:004010AE                 cmp     eax, esi        ; (k[4] + k[5]) >> 1 == serial[0] 
.text:004010B0                 jnz     PrintBadSerial
.text:004010B6                 push    offset aGoodSerial ; "Good Serial!"
.text:004010BB                 push    offset Format   ; "%s\n"
.text:004010C0                 call    _printf
.text:004010C5                 add     esp, 8
.text:004010C8                 xor     eax, eax
.text:004010CA                 pop     esi
.text:004010CB                 pop     ebx
.text:004010CC                 retn
```

I have added comments that describe the logic of this assembly code. It does some operations to specific characters from the serial argument. If the serial argument satisfies all of these conditions after some operations have been done, the program will finally print “Good Serial!”.   

**The Keygen** 

At this point, it is time to write a key generator.

From our static analysis, we can assume that:

- The serial must be length 10, otherwise it will jump to PrintBadSerial
- A character of the serial is subtracted by 0x20 if it is between 0x61 and 0x7a.
- serial[0] == serial[9]-3
- serial[1] == serial[8] + 14
- serial[2] == serial[7]-20
- serial[3] == serial[6]+ 6
- (k[4] + k[5]) >> 1 == serial[0]

Now it is time to test out Z3. How would I be able to satisfy these constraints?

```python
s, k = Solver(), []

for i in range(10):  # 10 character length 
    v = BitVec(f"v{i}", 8)  #  create bit-vector variable , v0, v1.. etc that is 8 bits 
    v_ = BitVec(f"v_{i}", 8)  #  create bit-vector variable , v_0, v_1.. etc that is 8 bits 
    # A character of the serial is subtracted by 0x20 if it is between 0x61 and 0x7a.
    s.add(Or(And(v_>96, v_<=122, v==v_-32), And(Or(v_<=96, v_>122), v==v_))) # subtract by 0x20  
    # variable values should be ascii values that can be typed from keyboard 
    s.add(And(v_>=32, v_<=126))
    # insert to list index: value 
    k.insert(i, v)
```

We can represent the assumptions we have made previously using Z3’s api. Two variables v and v_ that are 8 bits are created. This will be a possible character that will satisfy the conditions to create a valid serial.  If a variable v_ is between 96(0x61) and 122(0x7a),  v_ is subtracted by 0x20 and set to v. Otherwise v is equal to v_ (no change) . Next, we have to make sure that the characters are ASCII printable characters; which is why v_ should be between 32 and 126. 

```python
s.add(k[0] == k[9] - 3) 
s.add(k[1] == k[8] + 14)
s.add(k[2] == k[7] -20)
s.add(k[3] == k[6]+ 6)
s.add(k[0] == (LShR((k[4] + k[5]), 1)))
```

We have already created a possible 10 value serial. Now let us add other conditions some indexes of the serial have to satisfy. 

```python
if s.check() == sat:
    print(''.join(chr(int(x.split("=")[1])) for x in sorted(repr(s.model()).strip("[]").split(",\n "))[10:]))
```

Now let us solve and print our serial key

```
Good Serial!
```


**Conclusion**

In this write-up, I explained one of the possible solutions to this crack me using Z3.  Although this was a simple crack me, I was able to learn how Z3 can be applied in reverse-engineering.  Z3 can be quite a useful tool when brute force could take too long, or when the problem can easily be put in the form of equations. 

**Additional Resources**

[https://ericpony.github.io/z3py-tutorial/guide-examples.htm](https://ericpony.github.io/z3py-tutorial/guide-examples.htm)

[https://www.aldeid.com/wiki/X86-assembly/Instructions/cdq#:~:text=The CDQ](https://www.aldeid.com/wiki/X86-assembly/Instructions/cdq#:~:text=The%20CDQ%20) 

[https://theasciicode.com.ar/](https://theasciicode.com.ar/)